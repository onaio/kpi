grammar Kobo.Query
  
  #// To re-generate `grammar.py` from this file, use `canopy`.
  #// - Cli:  https://www.npmjs.com/package/canopy
  #// - Docs: http://canopy.jcoglan.com/langs/python.html
  #//
  #//     canopy 'grammar.peg' --lang python

  #--------------------

  query    <- _* exp? _*                              %query

  #--------------------

  exp      <- 
      andexp (_+ "OR" _+ expr:andexp)*                %orexp

  andexp   <- 
      groupexp ((_+ "AND")? _+ !"OR" expr:groupexp)*  %andexp
  
  groupexp <- 
       "(" _* exp _* ")"                              %parenexp
      / "NOT" _+ exp                                  %notexp
      / term

  #----------------------

  term      <- field:(name ":")? value:value   %term
  value     <- string
             / word
  word      <-  [^\s():]+                      %word
  string    <- '"' ("\\" . / [^"])* '"'        %string
             / "'" ("\\" . / [^'])* "'"        %string
  name      <- [a-zA-Z_] [a-zA-Z0-9\-_]*       %name
  _ <- [\s]
